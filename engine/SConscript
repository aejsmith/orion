import util

Import('manager')

# Always have the engine include directory available.
manager.baseEnv['CPPPATH'] += [Dir('include')]

# Add all 3rd party library components.
SConscript(dirs = ['3rdparty'])

# Build utilities.
SConscript(dirs = ['utilities'])

depends = [
    'external/sdl',
    'external/bullet',
    'external/freetype',
    '3rdparty/glm',
    '3rdparty/glslang',
    '3rdparty/imgui',
    '3rdparty/rapidjson',
]

if manager.baseEnv['GPU_API'] == 'gl':
    depends += [
        'external/glew',
        '3rdparty/spirv-cross',
    ]
elif manager.baseEnv['GPU_API'] == 'vulkan':
    depends += ['external/vulkan']

env = manager.CreateEnvironment(depends = depends)
Export('env')

manager.AddComponent(
    name = 'engine',
    depends = depends,
    objects = util.flatten(SConscript(dirs = [
        'include',
        'src',
    ])))
