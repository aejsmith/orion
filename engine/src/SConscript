from datetime import datetime
import util
import vcs

Import('env')

sources = util.flatten(SConscript(dirs = [
    'asset',
    'core',
    'gpu',
    'lib',
    'math',
    'render',
    'shader',
    'world',
]))

sources += map(File, [
    'version.cc',
])

# Generate the version information file.
def gen_version_cc(target, source, env):
    timestamp = datetime.now().strftime("%Y-%m-%d %H:%M:%S")
    version = vcs.revision_id()

    f = open(str(target[0]), 'w')
    f.write('const char *g_version_string = "%s";\n' % (version))
    f.write('const char *g_version_timestamp = "%s";\n' % (timestamp))
    f.close()
AlwaysBuild(env.Command('version.cc', [], env.Action(gen_version_cc, None)))

engine = env.Library('engine', sources)
Return('engine')
