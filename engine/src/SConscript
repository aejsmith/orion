from datetime import datetime
import util
import vcs

Import('env')

objects = util.flatten(SConscript(dirs = [
    'core',
    'engine',
    'gpu',
    'graphics',
    'input',
    'physics',
    'render',
    'shader',
]))

objects += map(env.Object, [
    'version.cc',
])

# Generate the version information file.
def gen_version_cc(target, source, env):
    timestamp = datetime.now().strftime("%Y-%m-%d %H:%M:%S")
    version = vcs.revision_id()

    f = open(str(target[0]), 'w')
    f.write('const char *g_versionString = "%s";\n' % (version))
    f.write('const char *g_versionTimestamp = "%s";\n' % (timestamp))
    f.close()
AlwaysBuild(env.Command('version.cc', [], env.Action(gen_version_cc, None)))

Return('objects')
